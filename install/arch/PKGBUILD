# Maintainer: Frodo45127 <frodo_gv@hotmail.com>
pkgname=('rpfm-git')
pkgver=3.99.106.2813.g3af6da7c
pkgrel=1
pkgdesc="Rusted PackFile Manager (RPFM) is a modding tool for modern (since Empire) Total War games."
arch=('x86_64')
url="https://github.com/Frodo45127/rpfm.git"
license=('MIT')
depends=('qt5-base' 'qt5-imageformats' 'xz' 'p7zip')
makedepends=('git' 'rust' 'cmake')
provides=('rpfm')
source=("git+https://github.com/Frodo45127/rpfm.git")
sha256sums=('SKIP')
_programname=('rpfm')

pkgver() {
    cd $_programname
    echo "$(grep '^version =' $srcdir/$_programname/Cargo.toml|cut -d\" -f2).$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
    cd $_programname
    env CARGO_INCREMENTAL=0 cargo build --release
}

package() {

    # The executables.
    install -D -m755 "$srcdir/$_programname/target/release/rpfm_ui" "$pkgdir/usr/bin/rpfm_ui"
    install -D -m755 "$srcdir/$_programname/target/release/rpfm_cli" "$pkgdir/usr/bin/rpfm_cli"

    # The icons.
    mkdir -p "$pkgdir/usr/share/$_programname/icons"
    for image in "$srcdir/$_programname/icons/."; do
        cp -R $image "$pkgdir/usr/share/$_programname/icons/"
    done

    # The language files.
    mkdir -p "$pkgdir/usr/share/$_programname/locale"
    for locale in "$srcdir/$_programname/locale/."; do
        cp -R $locale "$pkgdir/usr/share/$_programname/locale/"
    done

    # The UI files.
    mkdir -p "$pkgdir/usr/share/$_programname/ui"
    for ui_template in "$srcdir/$_programname/rpfm_ui/ui_templates/."; do
        cp -R $ui_template "$pkgdir/usr/share/$_programname/ui/"
    done

    # Shortcuts.
    install -D -m644 "$srcdir/$_programname/install/arch/rpfm.desktop" "$pkgdir/usr/share/applications/rpfm.desktop"

    # License.
    install -D -m644 "$srcdir/$_programname/LICENSE" "$pkgdir/usr/share/licenses/$_programname/LICENSE"

    # This one is not in the qt5-imageformats package by default since Qt 5.8.
    #install -D -m644 "$srcdir/$_programname/imageformats/libqdds.so" "$pkgdir/usr/lib/qt/plugins/imageformats/libqdds.so"
}
